# ProtoController v1.0 by Brackeys (Multiplayer patch)
extends CharacterBody3D

@export var can_move : bool = true
@export var has_gravity : bool = true
@export var can_jump : bool = true
@export var can_sprint : bool = true
@export var can_freefly : bool = false

@export_group("Speeds")
@export var look_speed : float = 0.002
@export var base_speed : float = 5.0
@export var jump_velocity : float = 5.0
@export var sprint_speed : float = 7.5
@export var freefly_speed : float = 25.0

@export_group("Input Actions")
@export var input_left : String = "ui_left"
@export var input_right : String = "ui_right"
@export var input_forward : String = "ui_up"
@export var input_back : String = "ui_down"
@export var input_jump : String = "ui_accept"
@export var input_sprint : String = "sprint"
@export var input_freefly : String = "freefly"

var mouse_captured : bool = false
var look_rotation : Vector2
var move_speed : float = 0.0
var freeflying : bool = false

@onready var head: Node3D = $Head
@onready var collider: CollisionShape3D = $Collider
@onready var weapon_manager = $Head/Camera3D/WeaponManager
@onready var crosshair = $CanvasLayer/Crosshair
@onready var sync: MultiplayerSynchronizer = $MultiplayerSynchronizer

var nearby_items: Array = []
var nearby_shop: Node = null
var _authority_applied: bool = false

func _ready() -> void:
	var peer_id = name.to_int() 
	if peer_id > 0:
		set_multiplayer_authority(peer_id)
	
	check_input_mappings()
	if multiplayer.has_multiplayer_peer():
		_setup_authority()
	#check_input_mappings()
	#look_rotation.y = rotation.y
	#look_rotation.x = head.rotation.x
	## Defer so set_multiplayer_authority() from game.gd is guaranteed to have run
	#call_deferred("_setup_authority")

func apply_authority():
	_authority_applied = false  # reset flag để apply_authority có thể chạy lại
	_setup_authority()
	print("apply_authority | my id: ", multiplayer.get_unique_id(), " | authority: ", get_multiplayer_authority(), " | is_auth: ", is_multiplayer_authority())
	if is_multiplayer_authority():
		capture_mouse()
		$Head/Camera3D.current = true
		$CanvasLayer.visible = true
		set_physics_process(true)
	else:
		$Head/Camera3D.current = false
		$CanvasLayer.visible = false
		set_physics_process(false)

func _setup_authority():
	print("_setup_authority | my id: ", multiplayer.get_unique_id(), " | authority: ", get_multiplayer_authority(), " | is_auth: ", is_multiplayer_authority())
	#if _authority_applied:
		#return
	#_authority_applied = true
	#if is_multiplayer_authority():
		#capture_mouse()
		#$Head/Camera3D.current = true
		#$CanvasLayer.visible = true
		#set_physics_process(true)
	#else:
		#$Head/Camera3D.current = false
		#$CanvasLayer.visible = false
		#set_physics_process(false)
	var is_auth = is_multiplayer_authority()
	set_physics_process(is_auth)
	set_process_unhandled_input(is_auth) # Đảm bảo input được bật/tắt đúng
	
	if is_auth:
		capture_mouse()
		$Head/Camera3D.current = true
		$CanvasLayer.visible = true
	else:
		$Head/Camera3D.current = false
		$CanvasLayer.visible = false

# ─── RPCs ────────────────────────────────────────────────────────────────────

@rpc("any_peer", "call_remote", "unreliable_ordered")
func sync_head_pitch(pitch: float):
	if not is_multiplayer_authority():
		head.rotation.x = pitch

@rpc("any_peer", "call_remote", "reliable")
func request_shoot():
	if not multiplayer.is_server(): return
	var weapon = weapon_manager.get_current_weapon()
	if weapon: weapon.shoot()
	confirm_shoot.rpc()

# Thành:
@rpc("any_peer", "call_local", "reliable")
func confirm_shoot():
	pass

@rpc("any_peer", "call_remote", "reliable")
func request_grab(item_path: NodePath):
	if not multiplayer.is_server(): return
	var item = get_node_or_null(item_path)
	if item: grab_item(item)

# ─── Input ───────────────────────────────────────────────────────────────────

func _unhandled_input(event: InputEvent) -> void:
	if not is_multiplayer_authority(): return
	if Input.is_mouse_button_pressed(MOUSE_BUTTON_LEFT):
		capture_mouse()
	if Input.is_key_pressed(KEY_ESCAPE):
		release_mouse()

	if Input.is_action_just_pressed("shoot"):
		if multiplayer.is_server():
			request_shoot()
		else:
			request_shoot.rpc_id(1)
	if Input.is_action_just_pressed("reload"):
		var weapon = weapon_manager.get_current_weapon()
		if weapon:
			weapon.reload()
	# interact — only handle once (removed duplicate block)
	if Input.is_action_just_pressed("interact"):
		if nearby_shop != null:
			nearby_shop.open_menu()
		elif nearby_items.size() > 0:
			if multiplayer.is_server():
				request_grab(nearby_items[0].get_path())
			else:
				request_grab.rpc_id(1, nearby_items[0].get_path())

	if Input.is_key_pressed(KEY_1):
		weapon_manager.equip_primary()
	if Input.is_key_pressed(KEY_2):
		weapon_manager.equip_secondary()

	if mouse_captured and event is InputEventMouseMotion:
		rotate_look(event.relative)

	if can_freefly and Input.is_action_just_pressed(input_freefly):
		if not freeflying:
			enable_freefly()
		else:
			disable_freefly()

# ─── Physics ─────────────────────────────────────────────────────────────────

func _physics_process(delta: float) -> void:
	if not is_multiplayer_authority(): return

	if can_freefly and freeflying:
		var input_dir := Input.get_vector(input_left, input_right, input_forward, input_back)
		var motion := (head.global_basis * Vector3(input_dir.x, 0, input_dir.y)).normalized()
		motion *= freefly_speed * delta
		move_and_collide(motion)
		return

	if has_gravity:
		if not is_on_floor():
			velocity += get_gravity() * delta

	if can_jump:
		if Input.is_action_just_pressed(input_jump) and is_on_floor():
			velocity.y = jump_velocity

	if can_sprint and Input.is_action_pressed(input_sprint):
		move_speed = sprint_speed
	else:
		move_speed = base_speed

	if can_move:
		var input_dir := Input.get_vector(input_left, input_right, input_forward, input_back)
		var move_dir := (transform.basis * Vector3(input_dir.x, 0, input_dir.y)).normalized()
		if move_dir:
			velocity.x = move_dir.x * move_speed
			velocity.z = move_dir.z * move_speed
		else:
			velocity.x = move_toward(velocity.x, 0, move_speed)
			velocity.z = move_toward(velocity.z, 0, move_speed)
	else:
		velocity.x = 0
		velocity.y = 0

	if is_multiplayer_authority():
		crosshair.set_moving(velocity.length() > 0.1)

	move_and_slide()

# ─── Look ────────────────────────────────────────────────────────────────────

func rotate_look(rot_input : Vector2):
	look_rotation.x -= rot_input.y * look_speed
	look_rotation.x = clamp(look_rotation.x, deg_to_rad(-85), deg_to_rad(85))
	look_rotation.y -= rot_input.x * look_speed
	transform.basis = Basis()
	rotate_y(look_rotation.y)
	head.transform.basis = Basis()
	head.rotate_x(look_rotation.x)
	sync_head_pitch.rpc(head.rotation.x)

# ─── Helpers ─────────────────────────────────────────────────────────────────

func grab_item(item) -> void:
	item.apply_effect(self)
	item.queue_free()
	print("Grabbed", item.item_type)

func enable_freefly():
	collider.disabled = true
	freeflying = true
	velocity = Vector3.ZERO

func disable_freefly():
	collider.disabled = false
	freeflying = false

func capture_mouse():
	Input.set_mouse_mode(Input.MOUSE_MODE_CAPTURED)
	mouse_captured = true

func release_mouse():
	Input.set_mouse_mode(Input.MOUSE_MODE_VISIBLE)
	mouse_captured = false

func check_input_mappings():
	if can_move and not InputMap.has_action(input_left):
		push_error("Movement disabled. No InputAction found for input_left: " + input_left)
		can_move = false
	if can_move and not InputMap.has_action(input_right):
		push_error("Movement disabled. No InputAction found for input_right: " + input_right)
		can_move = false
	if can_move and not InputMap.has_action(input_forward):
		push_error("Movement disabled. No InputAction found for input_forward: " + input_forward)
		can_move = false
	if can_move and not InputMap.has_action(input_back):
		push_error("Movement disabled. No InputAction found for input_back: " + input_back)
		can_move = false
	if can_jump and not InputMap.has_action(input_jump):
		push_error("Jumping disabled. No InputAction found for input_jump: " + input_jump)
		can_jump = false
	if can_sprint and not InputMap.has_action(input_sprint):
		push_error("Sprinting disabled. No InputAction found for input_sprint: " + input_sprint)
		can_sprint = false
	if can_freefly and not InputMap.has_action(input_freefly):
		push_error("Freefly disabled. No InputAction found for input_freefly: " + input_freefly)
		can_freefly = false
